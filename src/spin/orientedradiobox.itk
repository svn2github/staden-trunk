#
# Orientedradiobox
#
itk::usual Orientedradiobox {
    keep -background -borderwidth -cursor -disabledforeground \
	-foreground -labelfont -selectcolor
}

# ------------------------------------------------------------------
#                            ORIENTEDRADIOBOX
# ------------------------------------------------------------------
class Orientedradiobox {
    inherit iwidgets::Labeledframe

    constructor {args} {}

    itk_option define -disabledforeground \
	disabledForeground DisabledForeground {}
    itk_option define -selectcolor selectColor Background {}
    itk_option define -command command Command {}
###### Added ####   
    itk_option define -orient orient Orient "vertical"
###### Added ####
    public {
      method add {tag args}
      method buttonconfigure {index args}
      method delete {index}
      method deselect {index}
      method flash {index}
      method get {}
      method index {index}
      method insert {index tag args}
      method select {index}
    }

    protected method _command { name1 name2 opt }

    private {
      method gettag {index}      ;# Get the tag of the checkbutton associated
                                 ;# with a numeric index

      method _rearrange {}       ;# List of radiobutton tags in vertically. 
      method _rearrange_h {}     ;# List of radiobutton tags in horizontally.
      variable _buttons {}       ;# List of radiobutton tags.
      common _modes              ;# Current selection.
      variable _unique 0         ;# Unique id for choice creation.
    }
}

#
# Provide a lowercased access method for the Orientedradiobox class.
#
proc ::orientedradiobox {pathName args} {
    uplevel ::Orientedradiobox $pathName $args
}

#
# Use option database to override default resources of base classes.
#
option add *Orientedradiobox.labelMargin	10	widgetDefault
#option add *Orientedradiobox.labelFont     \
      "-Adobe-Helvetica-Bold-R-Normal--*-120-*-*-*-*-*-*"  widgetDefault
option add *Orientedradiobox.labelPos		nw	widgetDefault
option add *Orientedradiobox.borderWidth	2	widgetDefault
option add *Orientedradiobox.relief		groove	widgetDefault

# ------------------------------------------------------------------
#                        CONSTRUCTOR
# ------------------------------------------------------------------
body Orientedradiobox::constructor {args} {
    trace variable [scope _modes($this)] w [code $this _command]

    grid columnconfigure $itk_component(childsite) 0 -weight 1

    eval itk_initialize $args
}

# ------------------------------------------------------------------
#                            OPTIONS
# ------------------------------------------------------------------

# ------------------------------------------------------------------
# OPTION: -command
#
# Specifies a command to be evaluated upon change in the orientedradiobox
# ------------------------------------------------------------------
configbody Orientedradiobox::command {}

# ------------------------------------------------------------------
# OPTION: -orient
#
# Position radiobuttons either horizontally or vertically.
# ------------------------------------------------------------------
configbody Orientedradiobox::orient {

}

# ------------------------------------------------------------------
#                            METHODS
# ------------------------------------------------------------------

# ------------------------------------------------------------------
# METHOD: index index
#
# Searches the radiobutton tags in the orientedradiobox for the one with the
# requested tag, numerical index, or keyword "end".  Returns the 
# choices's numerical index if found, otherwise error.
# ------------------------------------------------------------------
body Orientedradiobox::index {index} {
    if {[llength $_buttons] > 0} {
        if {[regexp {(^[0-9]+$)} $index]} {
            if {$index < [llength $_buttons]} {
                return $index
            } else {
                error "Orientedradiobox index \"$index\" is out of range"
            }

        } elseif {$index == "end"} {
            return [expr [llength $_buttons] - 1]

        } else {
            if {[set idx [lsearch $_buttons $index]] != -1} {
                return $idx
            }

            error "bad Orientedradiobox index \"$index\": must be number, end,\
                    or pattern"
        }

    } else {
        error "Orientedradiobox \"$itk_component(hull)\" has no radiobuttons"
    }
}

# ------------------------------------------------------------------
# METHOD: add tag ?option value option value ...?
#
# Add a new tagged radiobutton to the orientedradiobox at the end.  The method 
# takes additional options which are passed on to the radiobutton
# constructor.  These include most of the typical radiobutton 
# options.  The tag is returned.
# ------------------------------------------------------------------
body Orientedradiobox::add {tag args} {
    itk_component add $tag {
        eval radiobutton $itk_component(childsite).rb[incr _unique] \
            -variable [list [scope _modes($this)]] \
            -anchor w \
            -justify left \
            -highlightthickness 0 \
            -value $tag $args
    } { 
      usual
      ignore -highlightthickness -highlightcolor
      rename -font -labelfont labelFont Font
    }
    lappend _buttons $tag
    grid $itk_component($tag)
    
####Added###############################
    set dir $itk_option(-orient)

    if { $dir == "horizontal" } {
	after idle [code $this _rearrange_h]
    } else {
    after idle [code $this _rearrange]
    }
    return $tag
####Added###############################
#    after idle [code $this _rearrange]
#    return $tag
}

# ------------------------------------------------------------------
# METHOD: insert index tag ?option value option value ...?
#
# Insert the tagged radiobutton in the orientedradiobox just before the 
# one given by index.  Any additional options are passed on to the
# radiobutton constructor.  These include the typical radiobutton
# options.  The tag is returned.
# ------------------------------------------------------------------
body Orientedradiobox::insert {index tag args} {
    itk_component add $tag {
        eval radiobutton $itk_component(childsite).rb[incr _unique] \
            -variable [list [scope _modes($this)]] \
            -highlightthickness 0 \
            -anchor w \
            -justify left \
            -value $tag $args
    } { 
      usual
      ignore -highlightthickness -highlightcolor
      rename -font -labelfont labelFont Font
    }
    set index [index $index]
    set before [lindex $_buttons $index]
    set _buttons [linsert $_buttons $index $tag]
    grid $itk_component($tag)

####Added###############################

    set dir $itk_option(-orient)
    if { $dir == "horizontal" } {
	after idle [code $this _rearrange_h]
    } else {
    after idle [code $this _rearrange]
    }
    return $tag
####Added###############################

#    after idle [code $this _rearrange]
#    return $tag
}

# ------------------------------------------------------------------
# METHOD: _rearrange
#
# Rearrange the buttons in the childsite frame using
# the grid geometry manager.
# ------------------------------------------------------------------
body Orientedradiobox::_rearrange {} {
    set index 0
    set master $itk_component(childsite)
    
    if {[set count [llength $_buttons]] > 0} {
	foreach tag $_buttons {
	    grid configure $itk_component($tag) -row $index -sticky nw
	    grid rowconfigure $master $index -weight 0
	    incr index
	}
	grid rowconfigure $master [expr $count-1] -weight 1
    }
}


# ------------------------------------------------------------------
# METHOD: _rearrange_h
#
# Rearrange the buttons in the childsite frame using
# the grid geometry manager.
# ------------------------------------------------------------------
body Orientedradiobox::_rearrange_h {} {
    set index 0
    set master $itk_component(childsite)
    
    if {[set count [llength $_buttons]] > 0} {
	foreach tag $_buttons {
	    grid configure $itk_component($tag) -row 0 -column $index -sticky nw 
	    grid columnconfigure $master $index -weight 1
	    incr index
	}
	grid columnconfigure $master [expr $count-1] -weight 0
    }
}

# ------------------------------------------------------------------
# METHOD: delete index
#
# Delete the specified radiobutton.
# ------------------------------------------------------------------
body Orientedradiobox::delete {index} {

    set tag [gettag $index]
    set index [index $index]

    destroy $itk_component($tag)

    set _buttons [lreplace $_buttons $index $index]

    if {$_modes($this) == $tag} {
        set _modes($this) {}
    }
####Added###############################
    set dir $itk_option(-orient)

    if { $dir == "horizontal" } {
	after idle [code $this _rearrange_h]
    } else {
    after idle [code $this _rearrange]
    }
    return $tag
####Added###############################
#    after idle [code $this _rearrange]
#    return
}

# ------------------------------------------------------------------
# METHOD: select index
#
# Select the specified radiobutton.
# ------------------------------------------------------------------
body Orientedradiobox::select {index} {
    set tag [gettag $index]
    $itk_component($tag) invoke
}

# ------------------------------------------------------------------
# METHOD: get
#
# Return the tag of the currently selected radiobutton.
# ------------------------------------------------------------------
body Orientedradiobox::get {} {
    return $_modes($this)
}

# ------------------------------------------------------------------
# METHOD: deselect index
#
# Deselect the specified radiobutton.
# ------------------------------------------------------------------
body Orientedradiobox::deselect {index} {
    set tag [gettag $index]
    $itk_component($tag) deselect
}

# ------------------------------------------------------------------
# METHOD: flash index
#
# Flash the specified radiobutton.
# ------------------------------------------------------------------
body Orientedradiobox::flash {index} {
    set tag [gettag $index]
    $itk_component($tag) flash  
}

# ------------------------------------------------------------------
# METHOD: buttonconfigure index ?option? ?value option value ...?
#
# Configure a specified radiobutton.  This method allows configuration 
# of radiobuttons from the Orientedradiobox level.  The options may have any 
# of the values accepted by the add method.
# ------------------------------------------------------------------
body Orientedradiobox::buttonconfigure {index args} { 
    set tag [gettag $index]
    eval $itk_component($tag) configure $args
}

# ------------------------------------------------------------------
# CALLBACK METHOD: _command name1 name2 opt 
#
# Tied to the trace on _modes($this). Whenever our -variable for our
# radiobuttons change, this method is invoked. It in turn calls
# the user specified tcl script given by -command.
# ------------------------------------------------------------------
body Orientedradiobox::_command { name1 name2 opt } {
    uplevel #0 $itk_option(-command)
}

# ------------------------------------------------------------------
# METHOD: gettag index
#
# Return the tag of the checkbutton associated with a specified
# numeric index
# ------------------------------------------------------------------
body Orientedradiobox::gettag {index} {
    return [lindex $_buttons [index $index]]
}

